buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.3"
    classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.16.0"
  }
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'


sourceCompatibility = "1.11"
targetCompatibility = "1.11"

allprojects { 
	repositories {
	    jcenter()
	    maven {
	        url "https://oss.sonatype.org/content/repositories/snapshots/"
	    }
        maven {
            url 'https://packages.confluent.io/maven/'
        }
	}
}

dependencies {
    // These dependencies is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.kafka:kafka-streams:2.4.0'

    // These dependencies are used internally, and not exposed to consumers on their own compile classpath.
    implementation ('io.confluent:kafka-streams-avro-serde:5.3.2') {
    	// Kafka client is already included from Kafka Streams in version 2.4.0
		exclude group: 'org.apache.kafka', module: 'kafka-clients' 
  	}
    implementation 'org.slf4j:slf4j-api:1.7.25'

    // Use JUnit test framework   
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
}

sourceSets {
	main {
	    java {
	        srcDir 'src/main/java'
	        srcDir 'src-gen/main/java'
	    }
	}
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
	publications {
	    mavenJava(MavenPublication) {
	        groupId 'org.industrial-devops'
	        artifactId 'titan-ccp-common-kafka'
	        version '0.0.1-SNAPSHOT'
	
	        from components.java
	
	        artifact(sourceJar) {
	            classifier = 'sources'
	        }
	        artifact(javadocJar) {
	            classifier = 'javadoc'
	        }
			pom {
				name = 'Titan Control Center - Common Kafka'
				description = 'A common Kafka library for microservices of the Titan Control Center'
				url = 'https://git.industrial-devops.org/titan/ControlCenterPrototype/common'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						name = 'SÃ¶ren Henning'
						email = 'soeren.henning@email.uni-kiel.de'
					}
				}
			    scm {
					url = 'https://git.industrial-devops.org/titan/ControlCenterPrototype/common'
			    }
	        }
	    }
	}
	repositories {
	    maven {
	        url "https://oss.sonatype.org/content/repositories/snapshots/"
	        credentials {
	            username project.hasProperty('sonatypeUsername') ? project.sonatypeUsername : ''
	            password project.hasProperty('sonatypePassword') ? project.sonatypePassword : ''
	        }
	    }
	}
}

eclipse {
    classpath {
       downloadSources=true
       downloadJavadoc=true
    }
}